plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}


dependencies {
    implementation project(':module-common')

    implementation project(':module-item')
    implementation project(':base-item')

    implementation project(':module-noti')
    implementation project(':base-noti')

    implementation project(':module-member')
    implementation project(':base-member')

    implementation project(':module-route')
    implementation project(':base-route')

    // WEB
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // PERSISTENCE
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'com.mysql:mysql-connector-j:8.0.33'
    implementation 'com.h2database:h2:2.3.232'
    implementation 'net.bytebuddy:byte-buddy:1.15.9'
    implementation 'net.bytebuddy:byte-buddy-agent:1.15.9'
    implementation 'org.redisson:redisson-hibernate-53:3.38.1'

    implementation 'net.logstash.logback:logstash-logback-encoder:8.0'
    testImplementation 'io.github.hakky54:logcaptor:2.9.3'
    implementation 'com.github.codemonstur:embedded-redis:1.4.3'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // WEB FLUX
    implementation 'org.springframework.boot:spring-boot-starter-webflux:3.3.4'

    // TEST
    testImplementation 'org.springframework.security:spring-security-test'
}

shadowJar {
    archiveBaseName.set("event-agent")
    archiveClassifier.set("all")
    zip64 true
    manifest {
        attributes(
                'Premain-Class': 'com.e205.byteBuddy.EventAgent',
                'Can-Redefine-Classes': 'true',
                'Can-Retransform-Classes': 'true'
        )
    }
}

tasks.named("jar").configure {
    archiveClassifier.set("all")
    from(shadowJar)
}

bootRun {
    enabled = true
    dependsOn shadowJar
    jvmArgs = ["-javaagent:${projectDir}/build/libs/event-agent-0.0.1-SNAPSHOT-all.jar"]
}

test {
    useJUnitPlatform()
    dependsOn shadowJar
    jvmArgs = ["-javaagent:${projectDir}/build/libs/event-agent-0.0.1-SNAPSHOT-all.jar"]
}
